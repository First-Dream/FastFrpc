import net.fabricmc.loom.task.RemapJarTask

plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '7.+'
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 16
}

dependencies {
    implementation project(":common")

    minecraft "com.mojang:minecraft:1.18.2"
    mappings "net.fabricmc:yarn:1.18.2+build.3:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.51.1+1.18.2"

    modImplementation "com.terraformersmc:modmenu:3.2.1"
    modImplementation "me.shedaniel.cloth:cloth-config-fabric:6.2.62"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    dependencies {
        include(project(':common'))

        exclude '/mappings/*'
    }
}

task remappedShadowJar(type: RemapJarTask) {
    dependsOn tasks.shadowJar
    input = tasks.shadowJar.archiveFile
    archivesBaseName = 'FastFrpc-Fabric-1.17-1.18'
}

tasks.assemble.dependsOn tasks.remappedShadowJar

artifacts {
    archives remappedShadowJar
    shadow shadowJar
}